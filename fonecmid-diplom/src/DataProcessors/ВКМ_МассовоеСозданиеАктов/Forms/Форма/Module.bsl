



#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не задан период для формирования актов!", ,"Объект.Период");
		Возврат;
	КонецЕсли;
	
	Элементы.ГрВыполнено.Видимость = Ложь;
	Элементы.ГруппаИдетОперация.Видимость = Истина;
	Элементы.ГрОшибки.Видимость = Ложь;
	
	ДлительнаяОперация = СоздатьАктыНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ТекстСообщения = "Выполняется создание актов......";
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьАктыНаСервере()
	 ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Результат = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьАкты", Объект.Период);
	Возврат Результат;

КонецФункции

 &НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ОбновитьТаблицуАктовСервер(Результат.АдресРезультата);
		ОповеститьФормы();
	
		Элементы.ГруппаИдетОперация.Видимость = Ложь;
		Если Результат.Сообщения.Количество() > 0 Тогда
			Элементы.ГрОшибки.Видимость = Истина;
		Иначе
			Элементы.ГрВыполнено.Видимость = Истина;
		КонецЕсли;
		Для Каждого Сообщения Из Результат.Сообщения Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(Сообщения.Текст, , Сообщения.Поле);
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьФормы()
	
	АктыСсылки = Новый Массив;
	Для Каждого Строка Из Объект.Договора Цикл
		АктыСсылки.Добавить(Строка.Акт);
	КонецЦикла;
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъектов(АктыСсылки);
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуАктовСервер(АдресВХ)
	Объект.Договора.Загрузить(ПолучитьИзВременногоХранилища(АдресВх));
КонецПроцедуры


#КонецОбласти

