
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Создать акты.
// 
// Параметры:
//  Период - структура
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Создать акты
Функция СоздатьАкты(Период) Экспорт
	
	//Первоночальное заполнение ТЧ (Договорами и Актами к ним если они есть)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ДоговорыКонтрагентов.Ссылка КАК Договор,
				   |	ДоговорыКонтрагентов.ВидДоговора,
				   |	ДоговорыКонтрагентов.ВКМ_ДатаНачала КАК ДатаНачала,
				   |	ВЫБОР
				   |		КОГДА ДоговорыКонтрагентов.ВКМ_ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
				   |			ТОГДА ДАТАВРЕМЯ(2999, 12, 31, 23, 59, 59)
				   |		ИНАЧЕ ДоговорыКонтрагентов.ВКМ_ДатаОкончания
				   |	КОНЕЦ КАК ДатаОкончания
				   |ПОМЕСТИТЬ ВТ_Договора
				   |ИЗ
				   |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
				   |ГДЕ
				   |	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
				   |	И (ДоговорыКонтрагентов.ВКМ_ДатаНачала <= &ДатаК
				   |	И ВЫБОР
				   |		КОГДА ДоговорыКонтрагентов.ВКМ_ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
				   |			ТОГДА ДАТАВРЕМЯ(2999, 12, 31, 23, 59, 59)
				   |		ИНАЧЕ ДоговорыКонтрагентов.ВКМ_ДатаОкончания
				   |	КОНЕЦ > &ДатаН)
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	РеализацияТоваровУслуг.Ссылка КАК Акт,
				   |	РеализацияТоваровУслуг.Договор
				   |ПОМЕСТИТЬ ВТ_Акты
				   |ИЗ
				   |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
				   |ГДЕ
				   |	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаН И &ДатаК
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТ_Договора.Договор,
				   |	ЕСТЬNULL(ВТ_Акты.Акт, ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)) КАК Акт
				   |ИЗ
				   |	ВТ_Договора КАК ВТ_Договора
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Акты КАК ВТ_Акты
				   |		ПО ВТ_Договора.Договор = ВТ_Акты.Договор";

	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("ДатаН", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаК", Период.ДатаОкончания);

	Результат = Запрос.Выполнить();
	Таблица = Результат.Выгрузить();

	
	//Обход ТЧ и формирование актов
	Счетчик = 0;
	Отказ = ложь;
	Для Каждого СтрокаТЧ Из Таблица Цикл

		ОбъектАкт = Неопределено;

		Если СтрокаТЧ.Акт <> Документы.РеализацияТоваровУслуг.ПустаяСсылка() Тогда
			ОбъектАкт = СтрокаТЧ.Акт.ПолучитьОбъект();
		Иначе
			ОбъектАкт = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТЧ.Договор, "Организация, Владелец");
			ОбъектАкт.Дата = Период.ДатаОкончания ;//ОбщегоНазначения.ТекущаяДатаПользователя();
			ОбъектАкт.Организация = ДанныеДоговора.Организация;
			ОбъектАкт.Контрагент = ДанныеДоговора.Владелец;
			ОбъектАкт.Договор = СтрокаТЧ.Договор;
			ОбъектАкт.Ответственный = Пользователи.ТекущийПользователь();
		КонецЕсли;
		
		
		ОбъектАкт.ВыполнитьАвтозаполнение(Отказ);
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		ОбъектАкт.СуммаДокумента = ОбъектАкт.Услуги.Итог("Сумма");

		Если ОбъектАкт.ПроверитьЗаполнение() Тогда
//			РежимПроведения = ?(ОбъектАкт.Проведен, РежимПроведенияДокумента.Неоперативный,
//				РежимПроведенияДокумента.Оперативный);
			РежимПроведения = РежимПроведенияДокумента.Неоперативный;
			ОбъектАкт.Записать(РежимЗаписиДокумента.Проведение, РежимПроведения);
		Иначе
			ОбъектАкт.Записать(РежимЗаписиДокумента.Запись);
			СтрокаШаблон = СтрШаблон("Отмена проведения документа ""%1"", не заполнены ключевые данные!",
				ОбъектАкт.Ссылка);
			ОбщегоНазначения.СообщитьПользователю(СтрокаШаблон, , "Объект.Договора[" + Счетчик + "].Акт");
		КонецЕсли;
		СтрокаТЧ.Акт = ОбъектАкт.Ссылка;
		Счетчик = Счетчик +1;

	КонецЦикла;
	
	
	Возврат Таблица;

КонецФункции


#КонецОбласти


#КонецЕсли
