
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда



#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если СписокСотрудников.Количество() = 0 Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("Отмена проведения! Список сотрудников пуст.");
		Возврат;
	КонецЕсли;
	
	СтавкаНДФЛ = Константы.ВКМ_СтавкаНДФЛ.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_НачислениеФиксированныхПремийСписокСотрудников.СуммаПремии) КАК СуммаПремии
	|ИЗ
	|	Документ.ВКМ_НачислениеФиксированныхПремий.СписокСотрудников КАК ВКМ_НачислениеФиксированныхПремийСписокСотрудников
	|ГДЕ
	|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить().Выгрузить();
	
	// регистр ВКМ_ДополнительныеНачисления
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Для Каждого ТекСтрокаСписокСотрудников из Результат Цикл
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрокаСписокСотрудников.Сотрудник;
		Движение.Начисленно = ТекСтрокаСписокСотрудников.СуммаПремии;
		Движение.Премия = ТекСтрокаСписокСотрудников.СуммаПремии;
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ_Премия;
		Движение.ПериодРегистрации = Дата;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.Сотрудник = ТекСтрокаСписокСотрудников.Сотрудник;
		Движение.Начисленно = ТекСтрокаСписокСотрудников.СуммаПремии * СтавкаНДФЛ / 100;
		Удержание = Движение.Начисленно;
		Движение.СтавкаНДФЛ = СтавкаНДФЛ;
		Движение.База = ТекСтрокаСписокСотрудников.СуммаПремии;
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = ТекСтрокаСписокСотрудников.Сотрудник;
		Движение.Сумма = ТекСтрокаСписокСотрудников.СуммаПремии - Удержание;
		
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти
#КонецЕсли


