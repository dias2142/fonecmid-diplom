

#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполненияФлага();
КонецПроцедуры

#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)
	
	Строка = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	РасчетДатОтпускаПриИзменении(Строка, "Н");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)
	Строка = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	РасчетДатОтпускаПриИзменении(Строка, "О");
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковКоличествоДнейПриИзменении(Элемент)
	Строка = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	РасчетДатОтпускаПриИзменении(Строка, "К");
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковПриИзменении(Элемент)
	ЗаполненияФлага();
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура АнализГрафика(Команда)
	
	Если Объект.ОтпускаСотрудников.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нет данных для анализа отпусков!");
		Возврат;
	КонецЕсли;
	
	АдресВХ =АнализГрафикаНаСервере(УникальныйИдентификатор);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресВХ", АдресВХ);
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РасчетДатОтпускаПриИзменении(ТекущиеДанные, ЧтоИзменилось)
	
	СекундВДне = 60*60*24;
	
	Если ЧтоИзменилось = "Н" Тогда //Изменилось поле ДатаНачала
		Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) Тогда
			Если ТекущиеДанные.КоличествоДней > 0 Тогда
				ТекущиеДанные.ДатаОкончания = ТекущиеДанные.ДатаНачала + (СекундВДне * (ТекущиеДанные.КоличествоДней -1));
			Иначе
				Если ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания)  Тогда
					Если ТекущиеДанные.ДатаОкончания > ТекущиеДанные.ДатаНачала Тогда
						ТекущиеДанные.КоличествоДней = (ТекущиеДанные.ДатаОкончания - ТекущиеДанные.ДатаНачала)/СекундВДне +1;
					Иначе
						ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка дат!");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТекущиеДанные.КоличествоДней = 0;
		КонецЕсли;
	ИначеЕсли ЧтоИзменилось = "О" Тогда //Изменилось поле ДатаОкончания
		Если ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания) Тогда
			Если ТекущиеДанные.КоличествоДней > 0 Тогда
				ТекущиеДанные.ДатаНачала = ТекущиеДанные.ДатаОкончания - (СекундВДне * (ТекущиеДанные.КоличествоДней -1));
			Иначе
				Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала)  Тогда
					Если ТекущиеДанные.ДатаОкончания > ТекущиеДанные.ДатаНачала Тогда
						ТекущиеДанные.КоличествоДней = (ТекущиеДанные.ДатаОкончания - ТекущиеДанные.ДатаНачала)/СекундВДне +1;
					Иначе
						ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка дат!");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТекущиеДанные.КоличествоДней = 0;
		КонецЕсли;
	ИначеЕсли ЧтоИзменилось = "К" Тогда //Изменилось поле количество дней
		Если ТекущиеДанные.КоличествоДней > 0 Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала)  Тогда
				ТекущиеДанные.ДатаОкончания = ТекущиеДанные.ДатаНачала + (СекундВДне * (ТекущиеДанные.КоличествоДней -1));
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания) Тогда
				ТекущиеДанные.ДатаНачала = ТекущиеДанные.ДатаОкончания - (СекундВДне * (ТекущиеДанные.КоличествоДней -1));
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) И ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания) Тогда
				Если ТекущиеДанные.ДатаОкончания > ТекущиеДанные.ДатаНачала Тогда
						ТекущиеДанные.КоличествоДней = (ТекущиеДанные.ДатаОкончания - ТекущиеДанные.ДатаНачала)/СекундВДне +1;
				Иначе
						ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка дат!");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


&НаСервере
Функция АнализГрафикаНаСервере(УИН)
	
	//ДокОбъект = РеквизитФормыВЗначение(Объект);
	Адрес = ПоместитьВоВременноеХранилище(Объект.ОтпускаСотрудников.Выгрузить(),УИН );
	
	Возврат Адрес;
	
КонецФункции

&НаКлиенте
Процедура ЗаполненияФлага()

СотрудникиДнейОтпуска = Новый Соответствие();
Для Каждого Строка Из Объект.ОтпускаСотрудников Цикл
	
	ДнейОтпуска = СотрудникиДнейОтпуска.Получить(Строка.Сотрудник);
	Если ДнейОтпуска = Неопределено Тогда
		СотрудникиДнейОтпуска.Вставить(Строка.Сотрудник, Строка.КоличествоДней);
	Иначе
		СотрудникиДнейОтпуска.Вставить(Строка.Сотрудник, ДнейОтпуска + Строка.КоличествоДней);
	КонецЕсли;
КонецЦикла;
Для Каждого Строка Из Объект.ОтпускаСотрудников Цикл
	ДнейОтпуска = СотрудникиДнейОтпуска.Получить(Строка.Сотрудник);
	Строка.Флаг = ДнейОтпуска > 28;
	КонецЦикла;

КонецПроцедуры


#КонецОбласти
