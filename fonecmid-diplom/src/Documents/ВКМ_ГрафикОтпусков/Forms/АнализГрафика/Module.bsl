


#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресВХ = Неопределено;
	Если НЕ Параметры.Свойство("АдресВХ",АдресВХ) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресВХ) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Список = ПолучитьИзВременногоХранилища(АдресВХ);
	
	ПоказатьДиаграммуГанта(Список, Диаграмма);
	
КонецПроцедуры
#КонецОбласти



#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура ПоказатьДиаграммуГанта(ТаблицаОтпусков, ДГанта)
	
	ДГанта.Обновление = Ложь;
	ДГанта.Очистить();
	
	ТаблицаСотрудники = ТаблицаОтпусков.Скопировать();
	ТаблицаСотрудники.Колонки.Добавить("Категория");
	Для Каждого Строка Из ТаблицаСотрудники Цикл
		Если Строка.Сотрудник.КатегорияСотрудника = Перечисления.ВКМ_КатегорияСотрудника.Специалист Тогда
			Строка.Категория = "Спец";
		Иначе
			Строка.Категория = "Упр";
		КонецЕсли;
	КонецЦикла;
	
		
	//ТаблицаСотрудники.ЗагрузитьКолонку(Сотрудники, "Сотрудник");
	ТаблицаСотрудники.Свернуть("Категория, Сотрудник");
	ТаблицаСотрудники.Сортировать("Категория, Сотрудник");
	
	ТекущаяСерия = ДГанта.УстановитьСерию("Отпуск");
	ТекущаяСерия.Цвет = WebЦвета.Аквамарин;
	ТекущаяСерия.Текст = "Отпуск";
	
	Для Каждого Сотрудник ИЗ ТаблицаСотрудники Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("Сотрудник", Сотрудник.Сотрудник);
		ОтпускаСотрудника = ТаблицаОтпусков.НайтиСтроки(Отбор);

		ТекущаяТочка = Дганта.УстановитьТочку(Сотрудник.Сотрудник);
		ТекущаяТочка.Текст = Сотрудник.Сотрудник;
	
		ТекущееЗначение = ДГанта.ПолучитьЗначение(ТекущаяТочка,ТекущаяСерия);
		Для Каждого Отпуск Из ОтпускаСотрудника Цикл
			ТекущийИнтервал = ТекущееЗначение.Добавить();
			ТекущийИнтервал.Начало = Отпуск.ДатаНачала;
			ТекущийИнтервал.Конец = Отпуск.ДатаОкончания;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого Точка Из ДГанта.Точки Цикл
		Если Точка.Значение.КатегорияСотрудника = Перечисления.ВКМ_КатегорияСотрудника.Специалист Тогда
			Точка.ЦветТекста = WebЦвета.СинийСоСтальнымОттенком;
			Точка.Текст = СтрШаблон("%1 (спец.)", Точка.Текст);
		Иначе
			Точка.Текст = СтрШаблон("%1 (админ.)", Точка.Текст);
		КонецЕсли;
	КонецЦикла;
	
	ДГанта.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Месяц;
	ДГанта.КратностьПериодическогоВарианта = 1;
	ДГанта.Обновление = Истина;
		

КонецПроцедуры

#КонецОбласти
