
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ,Режим)
	
	//Действия для проведения по обслуживаниюклиента
	
	//Проверка типа договора и срока его действия
	СтрокаСообщения = "";
	Если НЕ КлиентСДействующимАбонДоговором(Договор, СтрокаСообщения) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Отмена проведения! %1", СтрокаСообщения),,"Договор","Объект");
		Возврат;
	КонецЕсли;
	
	СтавкаЧасаПоДоговору = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор,"ВКМ_СтоимостьЧасаРаботы" );
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Для Каждого ТекСтрокаВыполненыеРаботы из ВыполненыеРаботы Цикл
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.КоличествоЧасов = ТекСтрокаВыполненыеРаботы.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = Движение.КоличествоЧасов * СтавкаЧасаПоДоговору;
	КонецЦикла;

	// Проведения для расчета ЗП
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиентаВыполненыеРаботы.Ссылка.Сотрудник КАК Сотрудник,
	|	ВКМ_ОбслуживаниеКлиентаВыполненыеРаботы.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту
	|ПОМЕСТИТЬ ВТ_Работы
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненыеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненыеРаботы
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиентаВыполненыеРаботы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Работы.Сотрудник КАК Сотрудник,
	|	ВТ_Работы.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
	|ИЗ
	|	ВТ_Работы КАК ВТ_Работы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Сотрудник) КАК
	|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВТ_Работы.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";
	Запрос.УстановитьПараметр("Ссылка",Ссылка );
	Запрос.УстановитьПараметр("Дата",Дата );
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();	
	
	// регистр ВКМ_ВыполненыеСотрудникомРаботы
	Движения.ВКМ_ВыполненыеСотрудникомРаботы.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПроцентОтРабот = NULL Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Отмена проведения! Сотруднику не задан процент от работ.");
			Возврат;
		КонецЕсли;
		Движение = Движения.ВКМ_ВыполненыеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Сотрудник;
		Движение.ЧасовКОплате = Выборка.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = Выборка.ЧасыКОплатеКлиенту * СтавкаЧасаПоДоговору * Выборка.ПроцентОтРабот/100 ;		
	КонецЦикла;
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Комментарий = СтрШаблон("Заявка закрыта: %1", Формат(ОбщегоНазначения.ТекущаяДатаПользователя(), "ДЛФ=DD;"));
	КонецЕсли;
	
	
	
КонецПроцедуры
#КонецОбласти



#Область СлужебныеПроцедурыИФункции

Функция КлиентСДействующимАбонДоговором(Договор, Сообщение)
	Результат = Истина;
	ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор,"ВидДоговора, ВКМ_ДатаНачала, ВКМ_ДатаОкончания" );
	Если НЕ ДанныеДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		Результат = Ложь;
		Сообщение = "Указан не абонентский договор.";
		Возврат Результат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеДоговора.ВКМ_ДатаОкончания) Тогда 
		ДанныеДоговора.ВКМ_ДатаОкончания = Дата(2099,12,31);
	КонецЕсли;
	
	Результат = Дата >= ДанныеДоговора.ВКМ_ДатаНачала И Дата <= ДанныеДоговора.ВКМ_ДатаОкончания;
	Если НЕ Результат Тогда
		Сообщение = "Дата документа не в ходит в срок действия указанного договора.";
	КонецЕсли;
	
	Возврат Результат; 
		
КонецФункции

#КонецОбласти


#КонецЕсли
